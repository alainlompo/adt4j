apply plugin:'application'

mainClassName = "com.github.sviperll.adt4j.examples.Main"

dependencies {
    compile(project(':adt4j-core'))
    compile(group: "com.google.code.findbugs", name: "annotations", version: "2.0.3")
}

ext {
    javacGeneratedSourcesDir = file("${buildDir}/generated-sources/javac/main/java")
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir javacGeneratedSourcesDir
        }
    }
}

compileJava {
    doFirst {
        javacGeneratedSourcesDir.mkdirs()
    }
    options.compilerArgs += ['-s', javacGeneratedSourcesDir]
    source = source.filter { p -> !p.getPath().startsWith(javacGeneratedSourcesDir.getPath()) }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                description "ADT4J examples package. ADT4J implements Algebraic Data Types in Java language using visitor pattern";
            }
        }
    }
}
